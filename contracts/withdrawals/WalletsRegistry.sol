pragma solidity 0.5.17;

import "@openzeppelin/upgrades/contracts/Initializable.sol";
import "../access/WalletsManagers.sol";
import "../validators/ValidatorsRegistry.sol";
import "./Wallet.sol";
import "./Withdrawals.sol";

/**
 * @title WalletsRegistry
 * WalletsRegistry creates and assigns wallets to validators.
 * The deposits and rewards generated by the validator will be withdrawn to the wallet it was assigned.
 * To reuse wallets multiple times for different validators, a user with an admin
 * role can reset the wallet when all the users have withdrawn their shares.
 */
contract WalletsRegistry is Initializable {
    /**
    * Structure to store information about the wallet assignment.
    * @param unlocked - indicates whether users can withdraw from the wallet.
    * @param validatorId - ID of the validator wallet is attached to.
    */
    struct WalletAssignment {
        bool unlocked;
        bytes32 validatorId;
    }

    // determines whether validator ID (public key hash) has been assigned any wallet.
    mapping(bytes32 => bool) public assignedValidators;

    // maps wallet address to the information about its assignment.
    mapping(address => WalletAssignment) public wallets;

    // stores list of available wallets.
    address[] private availableWallets;

    // address of the Admins contract.
    Admins private admins;

    // address of the WalletsManagers contract.
    WalletsManagers private walletsManagers;

    // address of the ValidatorsRegistry contract.
    ValidatorsRegistry private validatorsRegistry;

    // address of the Withdrawals contract.
    Withdrawals private withdrawals;

    /**
    * Event for tracking wallet new assignment.
    * @param validatorId - ID (public key hash) of the validator wallet is assigned to.
    * @param wallet - address of the wallet the deposits and rewards will be withdrawn to.
    */
    event WalletAssigned(bytes32 validatorId, address indexed wallet);

    /**
    * Event for tracking wallet resets.
    * @param wallet - address of the reset wallet.
    */
    event WalletReset(address wallet);

    /**
    * Event for tracking wallet unlocks.
    * @param validatorId - ID of the validator wallet is assigned to.
    * @param wallet - address of the unlocked wallet.
    * @param usersBalance - users balance at unlock time.
    */
    event WalletUnlocked(bytes32 validatorId, address indexed wallet, uint256 usersBalance);

    /**
    * Constructor for initializing the WalletsRegistry contract.
    * @param _admins - address of the Admins contract.
    * @param _walletsManagers - address of the WalletsManagers contract.
    * @param _validatorsRegistry - address of the Validators Registry contract.
    * @param _withdrawals - address of the Withdrawals contract.
    */
    function initialize(
        Admins _admins,
        WalletsManagers _walletsManagers,
        ValidatorsRegistry _validatorsRegistry,
        Withdrawals _withdrawals
    )
        public initializer
    {
        admins = _admins;
        walletsManagers = _walletsManagers;
        validatorsRegistry = _validatorsRegistry;
        withdrawals = _withdrawals;
    }

    /**
    * Function for assigning wallets to validators.
    * Can only be called by users with a wallets manager role.
    * @param _validatorId - ID (public key hash) of the validator wallet should be assigned to.
    */
    function assignWallet(bytes32 _validatorId) external {
        require(!assignedValidators[_validatorId], "Validator has already wallet assigned.");

        (uint256 validatorAmount, ,) = validatorsRegistry.validators(_validatorId);
        require(validatorAmount != 0, "Validator does not have deposit amount.");
        require(walletsManagers.isManager(msg.sender), "Permission denied.");

        address wallet;
        // Check whether previous wallets could be reused
        if (availableWallets.length > 0) {
            wallet = availableWallets[availableWallets.length - 1];
            availableWallets.pop();
        } else {
            wallet = address(new Wallet(withdrawals));
        }

        wallets[wallet].validatorId = _validatorId;
        assignedValidators[_validatorId] = true;

        emit WalletAssigned(_validatorId, wallet);
    }

    /**
    * Function for resetting wallets.
    * Can only be called by users with an admin role.
    * Must be called only when all the users have withdrawn their shares.
    * @param _wallet - address of the wallet to reset.
    */
    function resetWallet(address _wallet) external {
        require(admins.isAdmin(msg.sender), "Permission denied.");
        require(wallets[_wallet].validatorId != "", "Wallet has been already reset.");

        delete wallets[_wallet];
        availableWallets.push(_wallet);
        emit WalletReset(_wallet);
    }

    /**
    * Function for unlocking wallets.
    * Can only be called by Withdrawals contract.
    * Users will be able to withdraw their shares from unlocked wallet.
    * @param _wallet - address of the wallet to unlock.
    * @param _usersBalance - users balance at unlock time.
    */
    function unlockWallet(address payable _wallet, uint256 _usersBalance) external {
        require(msg.sender == address(withdrawals), "Permission denied.");
        require(!wallets[_wallet].unlocked, "Wallet is already unlocked.");

        wallets[_wallet].unlocked = true;
        emit WalletUnlocked(wallets[_wallet].validatorId, _wallet, _usersBalance);
    }
}
